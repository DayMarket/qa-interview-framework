plugins {
    id 'java'
}

wrapper {
    gradleVersion = '9.3.0'
}

repositories {
    mavenCentral()
}

group = 'uz.daymarket.qa_interview_framework'
version = '1.0-SNAPSHOT'


def allureVersion = '2.32.0'
def aspectJVersion = '1.9.25.1'
def junitVersion = '5.14.2'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-parameters')
}

configurations {
    agent {
        canBeResolved = true
        canBeConsumed = true
    }
}

dependencies {

    agent "org.aspectj:aspectjweaver:$aspectJVersion"

    implementation platform("io.qameta.allure:allure-bom:$allureVersion")
    testImplementation "io.qameta.allure:allure-junit5"

    // Main (src/main) dependencies
    implementation 'ch.qos.logback:logback-classic:1.5.25'
//    implementation 'ch.qos.logback:logback-core:1.5.25'
    implementation("com.squareup.okhttp3:okhttp:5.1.0")
    implementation 'io.qameta.allure:allure-okhttp3'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.0'
    implementation("org.assertj:assertj-core:3.27.3")
    implementation("io.qameta.allure:allure-assertj")
//    runtimeOnly("org.aspectj:aspectjweaver:1.9.24")

    // Test dependencies
    testImplementation platform("org.junit:junit-bom:$junitVersion")
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
}

jar {
    manifest {
        attributes(
                "Automatic-Module-Name": "io.qameta.allure.okhttp"
        )
    }
}

test {
    ignoreFailures = false
    useJUnitPlatform()
    jvmArgs = [ "-javaagent:${configurations.agent.singleFile}" ]
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        showStandardStreams = true
        exceptionFormat = 'full'
        showCauses = true
        showExceptions = true
        showStackTraces = true
    }

    systemProperty("junit.jupiter.extensions.autodetection.enabled", "true")
    systemProperty("junit.jupiter.execution.parallel.enabled", "true")
    systemProperty("junit.jupiter.execution.parallel.mode.default", "concurrent")
    systemProperty("junit.jupiter.execution.parallel.mode.classes.default", "concurrent")
    systemProperty("junit.jupiter.execution.parallel.config.strategy", "fixed")
    systemProperty("junit.jupiter.execution.parallel.config.fixed.parallelism", "12")
    systemProperty("junit.jupiter.execution.parallel.config.fixed.max-pool-size", "12")

    reports.html.outputLocation = layout.buildDirectory.dir("reports/tests")
}

tasks.named('test') {
    // disable up-to-date checks so tests run every invocation
    outputs.upToDateWhen { false }
}
